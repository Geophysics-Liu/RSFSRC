import os, sys, string
sys.path.append('../../framework')
import bldutil

progs = '''
test
'''

try:  # distributed version
    Import('env root bindir pkgdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../../api/c++/SConstruct')

inc = Glob('[a-z]*.h*')

if 'c++' in env.get('API',[]):
    env.Prepend(CPPPATH=['../../include'],
                LIBPATH=['../../lib'],
                LIBS=[env.get('DYNLIB','')+'rsf++',
                      env.get('DYNLIB','')+'rsf'])

mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    if 'c++' in env.get('API',[]):
        prog = env.Program(prog,map(lambda x: x + '.cc',sources))
        env.Depends(prog,inc)
    else:
        prog = env.RSF_Place('sf'+prog,None,var='C++')
    if root:
        env.Install(bindir,prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
    docs = map(lambda prog: env.Doc(prog,'M%s.cc' % prog,lang='c++'),
               Split(progs))
    env.Depends(docs,'#/framework/rsf/doc.py')
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
#####################################################################
